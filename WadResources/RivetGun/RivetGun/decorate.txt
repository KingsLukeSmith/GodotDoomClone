ACTOR RivetGun : Weapon 7932
{
   Inventory.PickupSound "misc/w_pkup"
   Inventory.PickupMessage "You got the Rivet Gun!"
   Weapon.AmmoType1 "Rivets"
   Weapon.AmmoType2 "Rivets"
   Weapon.AmmoGive 8
   Weapon.AmmoUse1 1
   Weapon.AmmoUse2 8
   Scale 0.7
   +ALT_AMMO_OPTIONAL
   States
   {
   Spawn:
      RVPU A -1
      Loop
   Ready:
      RTGN A 1 A_WeaponReady
      Loop
   Deselect:
      RTGN A 1 A_Lower
      Loop  
   Select:
      RTGN A 1 A_Raise
      Loop 
  Fire:
      TNT1 A 0 A_Jump(80,"PainlessBolt")
      TNT1 A 0 A_PlaySound("RIVTFIR")
	  TNT1 A 0 A_GunFlash
	  RTGN A 1 Bright A_FireCustomMissile("RivetProj", random(-2.2,3.6), 1, 7, 4, 0, random(-1, 1))
      RTGN BCDE 1
	  RTGN A 3
      RTGN A 1 A_ReFire
      Goto Ready	  
   PainlessBolt:
      TNT1 A 0 A_PlaySound("RIVTFIR")
	  TNT1 A 0 A_GunFlash
	  RTGN A 1 Bright A_FireCustomMissile("RivetProjPainless", random(-2.2,3.6), 1, 7, 4, 0, random(-1, 1))
      RTGN BCDE 1
	  RTGN A 3
      RTGN A 1 A_ReFire
      Goto Ready
   AltFire: //Check
      TNT1 A 0
	  TNT1 A 0 A_JumpIfInventory("Rivets",8,2)
	  RTGN A 3 A_WeaponReady(WRF_NOSECONDARY)
	  Goto Ready
      TNT1 A 0 A_PlaySound("RIVTFIR")
	  TNT1 A 0 A_GunFlash
	  RTGN A 1 Bright A_FireCustomMissile("RivetSpreadShot",0, 1, 7, 4)
      RTGN BCDE 1
	  RTGN A 12
      RTGN A 1 A_ReFire
      Goto Ready 		  
   Flash:
      TNT1 A 1 Bright A_Light1
	  TNT1 A 1 Bright A_Light0
    Stop
   }
}

ACTOR Rivets : Ammo 32257
{   
   Inventory.Amount 12
   Inventory.MaxAmount 225
   Ammo.BackpackAmount 20
   Ammo.Backpackmaxamount 450
   Inventory.Icon RVTIA0
   Inventory.PickupSound "RVTAMMG1"
   Inventory.PickupMessage "Rivet Pile"
   Scale 0.6
   States
   {
   Spawn:
      RVTP A -1
      Loop
   }
}  

ACTOR RivetRack : Rivets 32258
{   
   Inventory.Amount 25
   Inventory.PickupSound "RVTAMMG2"
   Inventory.PickupMessage "Rivet Bolt Rack"
   Scale 0.7
   States
   {
   Spawn:
      RVTR A -1
      Loop
   }
}

ACTOR RivetProj : FastProjectile
{
   Speed 40
   Radius 7
   Height 8
   PROJECTILE
   +THRUACTORS
   +NODAMAGETHRUST
   Scale 0.15
   DeathSound "RivetImpact"
   MissileType "RivetFlyRing"
   MissileHeight 8
   States
   {
   Spawn:
      BBLT A 1 Bright
	  TNT1 A 0 A_Explode(12,12,0,1,12) //Better for controlling damage than a projectile with +RIPPER.
	  BBLT A 1 Bright
	  TNT1 A 0 A_SpawnItem("RivetProjDMG",0,0,0)
      Loop
   Death:
      TNT1 A 0
      TNT1 A 0 A_SetScale(0.3)
	  TNT1 A 0 A_SpawnItemEx("RivetBoltReboundFX",0,0,3,random(-2,2),random(-2,2),random(3,9),random(0,359),32)
	  TNT1 A 0 A_SetTranslucent(0.8)
      RIVP A 2 BRIGHT
      RIVP BCDE 1 BRIGHT
      Stop
   }
}

ACTOR RivetProjDMG //Spawns as the bolts travel to damage enemies.
{                  //Main damage dealer for bosses, as the rivets do not have +FORCERADIUSDMG.
   Speed 2
   Radius 6
   Height 7
   Damage (7*random(2,5)) //Adjust damage to be slightly higher if using non-vanilla enemies. (7*random(3,6)) for example.
   PROJECTILE
   +BLOODSPLATTER
   +NODAMAGETHRUST
   States
   {
   Spawn:
      TNT1 A 1
      Stop
   Death:
      TNT1 A 0
      Stop
   }
}

ACTOR RivetProjPainless : FastProjectile  //+PAINLESS on this version to prevent stunlocking of enemies.
{
   Speed 40
   Radius 7
   Height 8
   PROJECTILE
   +THRUACTORS
   +PAINLESS
   +NODAMAGETHRUST
   Scale 0.15
   DeathSound "RivetImpact"
   MissileType "RivetFlyRing"
   MissileHeight 8
   States
   {
   Spawn:
      BBLT A 1 Bright
	  TNT1 A 0 A_Explode(12,12,0,1,12) //Better for controlling damage than a projectile with +RIPPER.
	  BBLT A 1 Bright
	  TNT1 A 0 A_SpawnItem("RivetProjDMGPainless",0,0,0)
      Loop
   Death:
      TNT1 A 0
      TNT1 A 0 A_SetScale(0.3)
	  TNT1 A 0 A_SpawnItemEx("RivetBoltReboundFX",0,0,3,random(-2,2),random(-2,2),random(3,9),random(0,359),32)
	  TNT1 A 0 A_SetTranslucent(0.8)
      RIVP A 2 BRIGHT
      RIVP BCDE 1 BRIGHT
      Stop
   }
}


ACTOR RivetProjDMGPainless : RivetProjDMG //Spawns as bolts travel to damage enemies. This version randomly spawns to prevent stunlocking.
{                                         //Main damage dealer for bosses, as the rivets do not have +FORCERADIUSDMG.
   +PAINLESS
   States
   {
   Spawn:
      TNT1 A 1
      Stop
   Death:
      TNT1 A 0
      Stop
   }
}

//ALT FIRE

ACTOR RivetSpreadShot : FastProjectile
{
   Speed 40
   Radius 15
   Height 8
   Damage 0
   PROJECTILE
   Scale 0.15
   DeathSound "RIVTCLXP"
   MissileType "RivetFlyRingB"
   MissileHeight 8
   ReactionTime 6
   +BLOODSPLATTER
   +NODAMAGETHRUST
   States
   {
   Spawn:
      BBLT A 1 Bright
	  TNT1 A 0 A_Countdown
      Loop
   Death:
	  TNT1 A 0
	  TNT1 A 0 A_Explode(35,60,0,1,35) 
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, 0, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, -50, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, 50, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, -90, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, 90, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, -130, CMF_AIMDIRECTION)
	  TNT1 A 0 A_CustomMissile("RivetSpreadProj", 0, 0, 130, CMF_AIMDIRECTION)
      TNT1 A 0 A_SetScale(0.8)
	  TNT1 A 0 A_SetTranslucent(0.8)
      CLSD ABCDEFGHI 1 Bright
      Stop
   }
}

ACTOR RivetSpreadProj
{
     Speed 40
     Radius 3
     Height 3
     Mass 10
	 DeathSound "RIVTCLXP"
     MissileType "RivetFlyRingB"
     MissileHeight 8
	 Scale 0.15
     PROJECTILE
	 +THRUACTORS
	 +BLOODSPLATTER
	 +NODAMAGETHRUST
	 BounceType Hexen
	 BounceCount 4
	 WallBounceFactor 1.2
	 BounceSound "RIVTPBNC"
     States
     {
     Spawn:
        BBLT A 1 Bright
	    TNT1 A 0 A_Explode(6,12,0,1,12)
		TNT1 A 0 A_Jump(200,3)
	    TNT1 A 0 A_SpawnItem("RivetProjSpreadDMG",0,0,0)
		TNT1 A 0 A_Jump(255,2)
		TNT1 A 0 A_SpawnItem("RivetProjSpreadPainlessDMG",0,0,0)
		TNT1 A 0
        Loop      
     Death: 
	    TNT1 A 0 A_SpawnItemEx("RivetBoltReboundFX",0,0,3,random(-2,2),random(-2,2),random(3,9),random(0,359),32)
	    TNT1 A 0
        TNT1 A 0 A_SetScale(0.4)
	    TNT1 A 0 A_SetTranslucent(0.8) 
        CLSD ABCDEFGHI 1 Bright
        Stop   
     }
}

ACTOR RivetProjSpreadDMG
{
   Speed 2
   Radius 6
   Height 7
   Damage (5*random(1,3))
   PROJECTILE
   +BLOODSPLATTER
   +NODAMAGETHRUST
   States
   {
   Spawn:
      TNT1 A 1
      Stop
   Death:
      TNT1 A 0
      Stop
   }
}

ACTOR RivetProjSpreadPainlessDMG : RivetProjSpreadDMG
{
   +PAINLESS
   States
   {
   Spawn:
      TNT1 A 1
      Stop
   Death:
      TNT1 A 0
      Stop
   }
}


ACTOR RivetFlyRing //Rivet projectile flight effect.
{
	+NOINTERACTION
	+NOCLIP
    +BRIGHT
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 0.06
	Scale 0.2
	States
	{
	Spawn:
		RIVF AAA 1
		RIVF A 1 A_SetScale(0.15)
		RIVF A 1 A_SetScale(0.10)
		Stop
	}
}

ACTOR RivetFlyRingB //Rivet projectile flight effect. Alt fire.
{
	+NOINTERACTION
	+NOCLIP
    +BRIGHT
	Radius 1
	Height 1
	Renderstyle Add
	Alpha 0.1
	Scale 0.2
	States
	{
	Spawn:
		CLSD AAA 1
		Stop
	}
}

ACTOR RivetBoltReboundFX
{
  Radius 1
  Height 1
  Speed 15
  PROJECTILE
  -NOGRAVITY
  +THRUACTORS
  Gravity 0.6
  Scale 0.15
  BounceType Doom
  BounceFactor 0.4
  BounceCount 3
  BounceSound "RivetRebound"
  States
  {
  Spawn:
     TNT1 A 0
	 TNT1 A 0 A_Jump(255,"Var1","Var2")
	 Loop 
  Var1: //Horizontal
     RBLT ABCDEFGH 1
	 Loop
  Var2: //Vertical
     VRLT ABCDEFGH 1
	 Loop
  Death:
     BBLT A 80
     BBLT A 1 A_FadeOut(0.02)
     Goto Death+1
  }
}